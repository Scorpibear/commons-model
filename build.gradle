buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'net.researchgate.release' version '2.4.1'
    id "com.jfrog.bintray" version "1.7"

}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
}


sourceCompatibility = 1.6
targetCompatibility = 1.6

description = 'EPAM Report portal. REST API model'

dependencies {
    compile 'io.swagger:swagger-annotations:1.5.3'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.7.3'

    compile 'com.fasterxml.jackson.core:jackson-databind:2.7.3'
    compile('com.google.guava:guava:19.0')

    compile('org.springframework.hateoas:spring-hateoas:0.18.0.RELEASE') {
        exclude(module: 'spring-core')
        exclude(module: 'spring-webmvc')
    }
    compile 'javax.validation:validation-api:1.1.0.Final'

    testCompile 'junit:junit:4.12'
}

release {
    preTagCommitMessage = 'Release commit: '
    tagCommitMessage = 'Creating tag: '
    newVersionCommitMessage = "New version commit: "
    tagCommitMessage = ''
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId 'com.epam.reportportal'
            artifactId "$project.name"
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'reportportal'
        name = "$project.name"
        desc = "$description"
        websiteUrl = 'https://reportportal.github.io/'
        issueTrackerUrl = "https://github.com/reportportal/$project.name/issues"
        vcsUrl = "https://github.com/reportportal/$project.name"
        githubRepo = "reportportal/$project.name"
        githubReleaseNotesFile = 'CHANGELOG.md'

        userOrg = 'reportportal'
        licenses = ['GPL-3.0']
    }
}
afterReleaseBuild.dependsOn bintrayUpload
